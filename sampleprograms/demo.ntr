scene $1 {
	setup:
		god visit is false
		if visit == false:
			say "  ___    _   __      _       _     _   _       _____                 _  "
			say " / _ \  | | / /     (_)     | |   | | ( )     |  _  |               | |  "
			say "/ /_\ \ | |/ / _ __  _  __ _| |__ | |_|/ ___  | | | |_   _  ___  ___| |_ "
			say "|  _  | |    \| '_ \| |/ _` | '_ \| __| / __| | | | | | | |/ _ \/ __| __|"
			say "| | | | | |\  \ | | | | (_| | | | | |_  \__ \ \ \/' / |_| |  __/\__ \ |_ "
			say "\_| |_/ \_| \_/_| |_|_|\__, |_| |_|\__| |___/  \_/\_\\__,_|\___||___/\__|"
			say "                        __/ |                                            "
			say "                       |___/                                             "
			exposition "You wake up in a dark room. There is a door to your right. What would you like to do?"
			visit is true
		else:
			exposition "You're in the same old room where you started. Did you go in a circle? Are you lost?"
		moves right($2)
	action:
		say "There is a door to your right. What would you like to do?"
	cleanup:
}

start: $1

scene $2 {
	setup:
		exposition "An open area stretches before you. You can hear a voice speak to you. Will you listen to the voice?"
		god thesword is sword("The Sword of Semantic Correctness")
		moves left($1), down($3), right($4)
	action:
		if pocket.has("sword"):
			say "AHO: You have the sword. There's no reason for you to be in this room."
		else:
			if (response == "no"):
				say "That's your problem. You never listen. Are you ready to listen now?"
			if (response == "yes"):
				say "The voice is coming from Alfred Aho!"
				say "'Hello, intrepid traveler,' says Alfred Aho. 'Here is a sword.'"
				pocket.add("sword", thesword)
	cleanup:
}

item sword(name){
	id is name
}

scene $3 {
	setup: 
		message is "You've walked into a closet. Better go back where you came from."
		say message
		moves up($2)
	action:
		say message
	cleanup:
}

scene $4{
	setup:
		exposition "There's a llama in the corner. It's the Pumping Llama!"
		say "'Baa,' says the Pumping Llama. What will you do?"
		god pumpingllama is llama("Pumping Llama")
		moves left($2), up($5)
	action:
		if not response:
            continue
		if (response == "kick the llama"):
			say "You're a terrible person. You deserve to lose this game."
			lose
		elif (response == "pet the llama" or response == "hug the llama" or response == "embrace the llama"):
			say "The llama bats its eyelashes at you."
	cleanup:
		pocket.add("Pumping Llama", pumpingllama)
		exposition "A Pumping Llama once lived in this room. Now he follows you around."
}

item llama(name){
	id is name
}

scene $5{
	setup:
		exposition "THERE IS A GIANT DRAGON. He's wearing a purple shirt that says 'Complexity of Compiler Design.' But you are no knight. What could you do to fight the dragon? Punch? Stab? Throw the llama at him?"
		moves down($4)
		dragon_hp is 5
	action:
			if not response:
            	continue
			if (response == "punch the dragon" or response == "punch dragon"):
				say "You tried to punch the dragon with your puny, puny fists. It doesn't work."
			elif (response == "stab the dragon" or response == "stab dragon") and (pocket.has("sword")):
				dragon_hp is dragon_hp - 1
			elif (response == "throw the llama" or response == "throw llama"):
				say "That's animal cruelty. And it won't help you."
			elif (response == "run away"):
				say "You should probably go back and look for something to help you fight the dragon."
			say "Dragon HP: " + str(dragon_hp)
			if (dragon_hp == 5):
				say "DRAGON: My flames of ambiguity shall engulf you!"
			elif (dragon_hp == 4):
				say "DRAGON: Look at you. Lost in a text-based dungeon with no company but a Pumping Llama. Pathetic."
				say "The dragon is hurt but still alive. Now what?"
			elif (dragon_hp == 3):
				say "DRAGON: I'm getting real tired of that sword of yours."
				say "The dragon is hurt but still alive. Now what?"
			elif (dragon_hp == 2):
				say "DRAGON: Why do you keep stabbing me?"
				say "The dragon is hurt but still alive. Now what?"
			elif (dragon_hp == 1):
				say "DRAGON: *cough* ... *cough* ..."
				say "The dragon is hurt but still alive. Now what?"
			elif (dragon_hp == 0):
				say "DRAGON: Curse you..."
				say "You've slain the Complexity Dragon!"
				win "You win!"
	cleanup:
}